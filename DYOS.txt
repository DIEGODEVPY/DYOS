//{$CLEO}
{$CLEO .csi}
{$NOSOURCE}
{$USE Android}
/** # Instale o actualize SB siempre con esto:
 * https://github.com/MatiDragon-YT/data
 * o descompile sin el codigo funte.
*/
nop
jump @Main
reg "By MatiDragon"
reg "v 0.1.1"
:Main
const
    ePara_PC      = 0
    ePara_Android = 1
end 

// ####################################################################
           
            // Ajustes de compatibilidad
            $ModoDeCompilacion = ePara_Android
                
            $EmularAndroid     = false
            /** Permite desplazarce por el menu, con
             *  botones que no se usan para eso, como estos:
             *  [7][8][9]
             *  [U][I][O]
             *  [J][K][L]
            */
            
// ####################################################################
const
    Numero_De_Paginas = $Numero_De_Paginas
    Pagina_Actual     = $Pagina_Actual
    
    none = 'DUMMY'
    any = -1

    ePara_Player = 0
    ePara_Actor  = 1
end           
var
    Numero_De_Paginas : int  
    Pagina_Actual : int
end
if $ModoDeCompilacion == ePara_PC
then 
    while true
        wait 0
        if $ONMISSION == 0
        then
            ResetVars() 
            while $ONMISSION == 0  
                wait 0   
                //Game.GetPositionOfAnalogueSticks(PAD.PLAYER1,a,s,d,f)
                //printf "%i %i" 10 a s  
                                      
                if
                    Game.IsTestCheat('MENU')    
                then
                    IniciarMenu()
                end
            end
        end
    end
else  
    ResetVars() 
    IniciarMenu() // para testear rapido
    while true
        wait 0 
        if and
            Button.IsPressed(PAD.DISPLAY, 1)
            Button.IsPressed(PAD.DISPLAY, 7)
        then
            IniciarMenu() // para testear rapido
        end
    end
end

:ResetVars
    Numero_De_Paginas = 1
    Pagina_Actual = 1
    
    $colPlayer = true
    $canMove = true
                    
    $ActorModel = 0                                                       
    int $numActores = 0
    int $indActor = 0
    $Actor($indActor, 101i) = 0
    $colActor($indActor,101i) = false
    $visActor($indActor,101i) = true
                         
    $CarModel = 400
    int $numAutos = 0
    int $indAuto = 0
    $Auto($indAuto, 101i) = 0  
    $colAuto($indAuto,101i) = false  
    $visAuto($indAuto,101i) = true
    
    $ObjectModel = 837
    int $numObjeto = 0
    int $indObjeto = 0
    $Objeto($indObjeto, 101i) = 0 
    $colObjeto($indObjeto,101i) = false 
    $visObjeto($indActor,101i) = true
    
    l = 0
    n = 1
return

const
    Menu_Del_Jugador    = 0
    Menu_De_Los_Actores = 1
    Menu_De_Los_Autos   = 2
    Menu_De_Los_Objetos = 3
    Menu_Del_Juego      = 4
end
            
:IniciarMenu
    Actor.GetPositionAndOffset($tempvar_X_coord, $tempvar_Y_coord, $tempvar_Z_coord, _Player, 0.0, 0.0, -1.0)
    $tempvar_Angle = Actor.Angle(_Player)
    Interior.GetActive($Active_Interior)   

    Display.SetRadar(false)
    if $canMove == true
    then
        $canMove = false
        if Actor.IsDriving(_Player)
        then 
            0@ = Actor.GetCurrentCar(_Player)
            Car.SetKeepPosition(0@, true)
            Car.SetCollision(0@, false)  
                
            if $ModoDeCompilacion == ePara_PC
            then Player.SetCanMove(_Char, false)
            else Player.SetCanEnterExitCars(_Char, false)
            end
        else    
            Player.SetCanMove(_Char, false)
            Actor.SetKeepPosition(_Player, true)                           
            Actor.SetCollisionDetection(_Player, false)
        end 
    end
    
    Text.UseCommands(true)
    
    AparecerMenu() 
    Text.PrintHelp('ZZZDYOS')  
    
    :MenuPrincipal
    Menu.SetColumn($PANEL, 0, 'M0000DS', 'M0000DQ', 'M0000DO', 'M0000DR', 'M0000DP', 'M0000DT', none, none, none, none, none, none,none)
    FrenarRetorno()
    FrenarSeleccion()
    repeat
        wait 0
        if EntrarAlMenu()
        then
            if $Seleccion == menu_del_jugador
            then {$I ./src/player/menu.txt}
            end   
            if $Seleccion == menu_de_los_actores
            then {$I ./src/actor/menu.txt} 
            end   
            if $Seleccion == menu_de_los_autos
            then {$I ./src/vehicle/menu.txt} 
            end 
            if $Seleccion == menu_de_los_objetos
            then {$I ./src/object/menu.txt}   
            end
            if $Seleccion == menu_del_juego
            then {$I ./src/game/menu.txt}   
            end
            FrenarSeleccion()
        end
    until SalirDelMenu()
    
    :CerrarElMenu
    EsconderMenu()
    Display.SetRadar(true)
    
    if $canMove == false
    then
        $canMove = true
        
        if Actor.IsDriving(_Player)
        then 
            0@ = Actor.GetCurrentCar(_Player)
            Car.SetKeepPosition(0@, false)
            Car.SetCollision(0@, true) 
            
            if $ModoDeCompilacion == ePara_PC
            then Player.SetCanMove(_Char, true)
            else Player.SetCanEnterExitCars(_Char, true)
            end
        else    
            Player.SetCanMove(_Char, true)  
            Actor.SetKeepPosition(_Player, false) 
            Actor.SetCollisionDetection(_Player, true)
        end 
    end
    if $colPlayer == false
    then
        Actor.SetKeepPosition(_Player, true) 
        Actor.SetCollisionDetection(_Player, false) 
    end
     
    Text.UseCommands(false)     
    
    Text.ClearHelp()
return 

:AparecerMenu
    $PANEL = Menu.Create(none, 29.0, 154.0, 180.0, 1, true, true, Align.Left) 
return    
:EsconderMenu
    Menu.Remove($PANEL)
return
        

// CREADORES
{$I ./src/actor/create.txt}
{$I ./src/vehicle/create.txt}
{$I ./src/object/create.txt}

// CONTROL DE LOS MENUS
{$I ./src/ControlesDelMenu.txt}
{$I ./src/ControlDeDesplazamiento3D.txt}

:ImpresionDeLaPaginacion
    if $ModoDeCompilacion == ePara_PC
    then Text.DisplayWith2Numbers(170.0,179.0,'M0000BG',Pagina_Actual,Numero_De_Paginas) 
    else Text.DisplayWith2Numbers(130.0,179.0,'M0000BG',Pagina_Actual,Numero_De_Paginas) 
    end   
return

:FrenarSeleccion
    while EntrarAlMenu()
        wait 0
        ImpresionDeLaPaginacion()
    end
return

:FrenarPaginacion 
    while MovimientoVerticalDelMenu()
        wait 0
        ImpresionDeLaPaginacion()
    end
return

:FrenarRetorno
    while SalirDelMenu()
        wait 0
        ImpresionDeLaPaginacion()
    end
return

:DarArmaConMunicion {
        param1 - Actor
        param2 - Model ID
        param3 - Weapon ID
        param4 - Ammo
    }
    Model.Load(param2)
    repeat
        wait 0
    until Model.Available(param2)

    if param1 == ePara_Player
    then Actor.setGiveWeaponAndAmmo(_Player, param3, param4)
    else Actor.SetGiveWeaponAndAmmo($Actor($indActor,101i), param3, param4)
    end
    
    Model.Release(param2)
return

:MensajeDeAplicado
    Text.ClearHelp()
    Text.PrintHelp('ZUIZAPL')
return