//{$CLEO}
{$CLEO .csi}
{$NOSOURCE}
{$USE Android}
/** # Instale o actualize SB siempre con esto:
 * https://github.com/MatiDragon-YT/data
 * o descompile sin el codigo funte.
*/
nop
jump @Main
reg "By MatiDragon"
reg "v 0.3-beta.5"
:Main                         

// ####################################################################
           
           
            // Ajustes de compatibilidad  
            //$ModoDeCompilacion = ePara_PC
            $ModoDeCompilacion = ePara_Android
           
            
// ####################################################################          
var
    $Pagina_Actual : int  
    $Numero_De_Paginas : int
    $tempvar_X_coord : float
    $tempvar_Y_coord : float
    $tempvar_Z_coord : float
end
if $ModoDeCompilacion == ePara_PC
then 
    //0A95:  save_progress
    while true
        wait 0
        while _OnMission == 1   
            wait 0
        end
        
        ResetVars() 
        while _OnMission == 0 
            wait 0                
            if
                Game.IsTestCheat('MENU')    
            then
                IniciarMenu()
            end
            thread_externo()
        end
    end
else  
    ResetVars() 
    ButtonResetPosition()   
    
    IniciarMenu() // para testear rapido
    while true
        wait 0 
        if and
            Button.IsPressed(PAD.DISPLAY, 1)
            Button.IsPressed(PAD.DISPLAY, 7)
        then
            IniciarMenu() // para testear rapido
        end     
        thread_externo()
    end
end

:ResetVars    
    $Numero_De_Paginas = 1
    $Pagina_Actual = 1
                  
    int $ActorModel = 0                                                       
    int $numActores = 0
    int $indActor = 0
    $Actor($indActor,101i) = 0
                         
    int $CarModel = 400
    int $numAutos = 0
    int $indAuto = 0
    $Auto($indAuto,101i) = 0  
    
    int $ObjectModel = 321
    int $numObjeto = 0
    int $indObjeto = 0
    $Objeto($indObjeto,101i) = 0  
                      
    int $cinematicMode = 0
    int $CamType = 0
    float $CamShake = 0.0
    int $CamHit = 0
    int $CamMareo = 0

    int $player_infinityHealth = false
    int $timeStoped = false
    
    l = 0
    n = 1   
return

const
    Menu_Del_Jugador      = 0
    Menu_De_Los_Actores   = 1
    Menu_De_Los_Vehiculos = 2
    Menu_De_Los_Objetos   = 3 
    Menu_De_La_Camara     = 4
    Menu_Del_Juego        = 5
end            
:IniciarMenu    
    GetUpdatePlayerPosition() 
    
    Display.SetRadar(false)

    if not Actor.IsDriving(_Player)
    then Actor.SetCollisionDetection(_Player, false)
    end 
    Player.SetCanMove(_Char, false)
    
    Text.UseCommands(true)
    
    AparecerMenu() 
    Text.ClearHelp()
    Text.PrintHelp('ZZZDYOS')  
    
    :MenuPrincipal
    Menu.SetColumn($PANEL, 0, 'M0000DS', 'M0000DQ', 'M0000DO', 'M0000DR', 'M0000DP', 'M0000IR', 'M0000DT', none, none, none, none, none,none)
    
    FrenarRetorno()
    FrenarSeleccion()
    repeat
        wait 0 
        if EntrarAlMenu()
        then
            if _Selected == menu_del_jugador
            then {$I ./src/player/menu.txt}
            end   
            if _Selected == menu_de_los_actores
            then {$I ./src/actor/menu.txt} 
            end        
            if _Selected == menu_de_los_vehiculos
            then {$I ./src/vehicle/menu.txt} 
            end 
            if _Selected == menu_de_los_objetos
            then {$I ./src/object/menu.txt}   
            end                
            if _Selected == menu_de_la_camara
            then {$I ./src/camera/menu.txt}
            end  
            if _Selected == menu_del_juego
            then {$I ./src/game/menu.txt}   
            end
            FrenarSeleccion()
        end
    until SalirDelMenu()
    
    :CerrarElMenu
    EsconderMenu()
    Display.SetRadar(true)
    
    FrenarRetorno()
    
    if not Actor.IsDriving(_Player)
    then Actor.SetCollisionDetection(_Player, true)
    end 
    Player.SetCanMove(_Char, true)
     
    Text.UseCommands(false)     
    
    Text.ClearHelp()
return

{$I ./src/thread_externo.txt}
{$I ./src/thread_aislado.txt}  
 