{$CLEO}
//{$CLEO .csi}
{$NOSOURCE}
{$USE Android}
{$USE File}
/** # Instale o actualize SB siempre con esto:
 * https://github.com/MatiDragon-YT/data
 * o descompile sin el codigo funte.
*/
nop
jump @Main
reg "By MatiDragon"
reg "v 0.3-beta.8"
:Main                         

// ####################################################################
           
           
            // Ajustes de compatibilidad  
            $ModoDeCompilacion = ePara_PC
            //$ModoDeCompilacion = ePara_Android
           
            
// ####################################################################  
        
var
    $Pagina_Actual : int  
    $Numero_De_Paginas : int
    $tempvar_X_coord : float
    $tempvar_Y_coord : float
    $tempvar_Z_coord : float
end
if $ModoDeCompilacion == ePara_PC
then
    //0A95:  save_progress
    while true
        wait 0
        while _OnMission == 1   
            wait 0
        end
        
        ResetVars() 
        while _OnMission == 0 
            wait 0                
            if
                Game.IsTestCheat('MENU')    
            then
                IniciarMenu()
            end
            thread_externo()
        end
    end
else  
    ResetVars() 
    ButtonResetPosition()   
    
    IniciarMenu() // para testear rapido
    while true
        wait 0 
        if and
            Button.IsPressed(PAD.DISPLAY, 1)
            Button.IsPressed(PAD.DISPLAY, 7)
        then
            IniciarMenu() // para testear rapido
        end     
        thread_externo()
    end
end

{$I ./src/const.txt}

:ResetVars    
    $Numero_De_Paginas = 1
    $Pagina_Actual = 1
                  
    int $ActorModel = 0                                                       
    int $numActores = 0
    int $indActor = 0
    $Actor($indActor,101i) = 0
                         
    int $CarModel = 400
    int $numAutos = 0
    int $indAuto = 0
    $Auto($indAuto,101i) = 0  
    
    int $ObjectModel = 321
    int $numObjeto = 0
    int $indObjeto = 0
    $Objeto($indObjeto,101i) = 0  
                      
    int $cinematicMode = 0
    int $CamType = 0
    float $CamShake = 0.0
    int $CamHit = 0
    int $CamMareo = 0 

    int $player_infinityHealth = false
    
    float $CamFov = 70.0
    int $colPlayer = true
    l = 0
    n = 1
    
    0@ = 1.0
    1@ = false
    2@ = false
    
    3@ = true
    4@ = true
    5@ = true
    6@ = true
    7@ = true
    8@ = true
    9@ = true
    10@ = 1.0
    11@ = 1.0
    12@ = false
    13@ = false
    14@ = false
    15@ = false
    16@ = false
    if $ModoDeCompilacion == ePara_PC
    then
        set_cleo_shared_var JUEGO_VELOCIDAD = 0@
        set_cleo_shared_var JUEGO_VELOCIDAD_CAMBIADA = 1@
        set_cleo_shared_var JUEGO_TIEMPO_DETENIDO = 2@
        set_cleo_shared_var JUEGO_TRAFICO_AVES = 3@
        set_cleo_shared_var JUEGO_TRAFICO_AEREO = 4@
        set_cleo_shared_var JUEGO_TRAFICO_AMBULANCIA = 5@
        set_cleo_shared_var JUEGO_TRAFICO_PANDILLAS = 6@
        set_cleo_shared_var JUEGO_TRAFICO_POLICIAS_CICLISTAS = 7@
        set_cleo_shared_var JUEGO_TRAFICO_POLICIAS_PATRULLEROS = 8@
        set_cleo_shared_var JUEGO_TRAFICO_TRENES = 9@
        set_cleo_shared_var JUEGO_DENCIDAD_ACTORES = 10@
        set_cleo_shared_var JUEGO_DENCIDAD_VEHICULOS = 11@
        set_cleo_shared_var JUEGO_CASAS_ROBABLES = 12@
        set_cleo_shared_var JUEGO_CASAS_LIBRES = 13@
        set_cleo_shared_var JUEGO_AMBIENTE_CAOTICO = 14@
        set_cleo_shared_var JUEGO_GUERRA_PANDILLAS = 15@
        set_cleo_shared_var JUEGO_TAXIS_NITROSOS = 16@
    else
        Android.SetVar(JUEGO_VELOCIDAD, 0@)
        Android.SetVar(JUEGO_VELOCIDAD_CAMBIADA, 1@)
        Android.SetVar(JUEGO_TIEMPO_DETENIDO, 2@) 
        Android.SetVar(JUEGO_TRAFICO_AVES, 3@)
        Android.SetVar(JUEGO_TRAFICO_AEREO, 4@)
        Android.SetVar(JUEGO_TRAFICO_AMBULANCIA, 5@)
        Android.SetVar(JUEGO_TRAFICO_PANDILLAS, 6@)
        Android.SetVar(JUEGO_TRAFICO_POLICIAS_CICLISTAS, 7@)
        Android.SetVar(JUEGO_TRAFICO_POLICIAS_PATRULLEROS, 8@)
        Android.SetVar(JUEGO_TRAFICO_TRENES, 9@)
        Android.SetVar(JUEGO_DENCIDAD_ACTORES, 10@)
        Android.SetVar(JUEGO_DENCIDAD_VEHICULOS, 11@) 
        Android.SetVar(JUEGO_CASAS_ROBABLES, 12@)
        Android.SetVar(JUEGO_CASAS_LIBRES, 13@)
        Android.SetVar(JUEGO_AMBIENTE_CAOTICO, 14@)
        Android.SetVar(JUEGO_GUERRA_PANDILLAS, 15@)
        Android.SetVar(JUEGO_TAXIS_NITROSOS, 16@)
    end   
return
           
:IniciarMenu    
    GetUpdatePlayerPosition() 
    
    Display.SetRadar(false)

    if not Actor.IsDriving(_Player)
    then Actor.SetCollisionDetection(_Player, false)
    end 
    Player.SetCanMove(_Char, false)
    
    Text.UseCommands(true)
    
    AparecerMenu() 
    Text.ClearHelp()
    Text.PrintHelp('ZZZDYOS')  
    
    :MenuPrincipal
    Menu.SetColumn($PANEL, 0, 'M0000DS', 'M0000DQ', 'M0000DO', 'M0000DR', 'M0000DP', 'M0000IR', 'M0000DT', none, none, none, none, none,none)
    
    FrenarRetorno()
    FrenarSeleccion()
    repeat
        wait 0 
        if EntrarAlMenu()
        then
            if _Selected == menu_del_jugador
            then {$I ./src/player/menu.txt}
            end   
            if _Selected == menu_de_los_actores
            then {$I ./src/actor/menu.txt} 
            end        
            if _Selected == menu_de_los_vehiculos
            then {$I ./src/vehicle/menu.txt} 
            end 
            if _Selected == menu_de_los_objetos
            then {$I ./src/object/menu.txt}   
            end                
            if _Selected == menu_de_la_camara
            then {$I ./src/camera/menu.txt}
            end  
            if _Selected == menu_del_juego
            then {$I ./src/game/menu.txt}   
            end
            FrenarSeleccion()
        end
    until SalirDelMenu()
    
    :CerrarElMenu
    EsconderMenu()
    Display.SetRadar(true)
    
    FrenarRetorno()
    
    if not Actor.IsDriving(_Player)
    then Actor.SetCollisionDetection(_Player, true)
    end 
    if $ModoDeCompilacion == ePara_PC
    then
        if Actor.IsDriving(_Player)
        then FrenarRetorno()
        end
    end 
    Player.SetCanMove(_Char, true)
     
    Text.UseCommands(false)     
    
    Text.ClearHelp()
return

{$I ./src/thread_externo.txt}
{$I ./src/thread_aislado.txt}  
 