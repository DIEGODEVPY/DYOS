//{$CLEO}
{$CLEO .csi}
{$NOSOURCE}
{$USE Android}
/** # Instale o actualize SB siempre con esto:
 * https://github.com/MatiDragon-YT/data
 * o descompile sin el codigo funte.
*/
nop
jump @Main
reg "By MatiDragon"
reg "v 0.3-beta.2"
:Main

// ####################################################################
           
            // Ajustes de compatibilidad
            $ModoDeCompilacion = ePara_Android
                
            $EmularAndroid     = false
            /** Permite desplazarce por el menu, con
             *  botones que no se usan para eso, como estos:
             *  [7][8][9]
             *  [U][I][O]
             *  [J][K][L]
            */
            
// ####################################################################          
var
    $Pagina_Actual : int  
    $Numero_De_Paginas : int
    $tempvar_X_coord : float
    $tempvar_Y_coord : float
    $tempvar_Z_coord : float
end
if $ModoDeCompilacion == ePara_PC
then 
    while true
        wait 0
        if _OnMission == 0
        then
            ResetVars() 
            while true 
                wait 0                
                if
                    Game.IsTestCheat('MENU')    
                then
                    IniciarMenu()
                end
                thread_externo()
            end
        end
    end
else  
    ResetVars() 
    ButtonResetPosition()   
    
    IniciarMenu() // para testear rapido
    while true
        wait 0 
        if and
            Button.IsPressed(PAD.DISPLAY, 1)
            Button.IsPressed(PAD.DISPLAY, 7)
        then
            IniciarMenu() // para testear rapido
        end     
        thread_externo()
    end
end

:ResetVars
    $Numero_De_Paginas = 1
    $Pagina_Actual = 1
    
    $colPlayer = true
    $canMove = true
    $indPlayer = false 
                    
    $ActorModel = 0                                                       
    int $numActores = 0
    int $indActor = 0
    $Actor($indActor,101i) = 0
    $colActor($indActor,101i) = false
    $visActor($indActor,101i) = true
    $indActor($indActor,101i) = false
                         
    $CarModel = 400
    int $numAutos = 0
    int $indAuto = 0
    $Auto($indAuto,101i) = 0  
    $colAuto($indAuto,101i) = false  
    $visAuto($indAuto,101i) = true   
    $indAuto($indAuto,101i) = false 
    
    $ObjectModel = 837
    int $numObjeto = 0
    int $indObjeto = 0
    $Objeto($indObjeto,101i) = 0 
    $colObjeto($indObjeto,101i) = false 
    $visObjeto($indActor,101i) = true
    
    int $cinematicMode = 0
    int $CamType = 0
    float $CamShake = 0.0
    int $CamHit = 0
    
    l = 0
    n = 1
    
    int $indPath = 0
    float $PathX($indPath,9f) = 0.0
    float $PathY($indPath,9f) = 0.0
    float $PathZd($indPath,9f) = 0.0
    
    float $gameSpeed = 1.0
return

const
    Menu_Del_Jugador      = 0
    Menu_De_Los_Actores   = 1
    Menu_De_Los_Vehiculos = 2
    Menu_De_Los_Objetos   = 3 
    Menu_De_La_Camara     = 4
    Menu_Del_Juego        = 5
end            
:IniciarMenu
    GetUpdatePlayerPosition() 
    
    Display.SetRadar(false)
    if $canMove == true
    then
        $canMove = false    
        //Player.SetCanMove(_Char, false)   
        //Actor.SetLockInCurrentPosition(_Player, true)
        
        Actor.SetKeepPosition(_Player, true)    
        if Actor.IsDriving(_Player)
        then 
            0@ = Actor.GetCurrentCar(_Player)
            Car.SetKeepPosition(0@, true)
            Car.SetCollision(0@, false)
            Player.SetCanMove(_Char, false)
        else
            if $ModoDeCompilacion == ePara_PC
            then
                Player.SetCanEnterExitCars(_Char,false)
                Player.SetCanJump(_Char, false)
                Player.SetCanCrouch(_Char, false)  
                Player.SetAbleToShootWeapons(_Char, false)
                Player.SetCycleWeapon(_Char, false) 
                Player.SetIgnoredByCops(_Char, true)
            else
                Player.SetCanMove(_Char, false)
            end
            Actor.SetCollisionDetection(_Player, false)
        end
    end
    
    Text.UseCommands(true)
    
    AparecerMenu() 
    Text.PrintHelp('ZZZDYOS')  
    
    :MenuPrincipal
    Menu.SetColumn($PANEL, 0, 'M0000DS', 'M0000DQ', 'M0000DO', 'M0000DR', 'M0000DP', 'M0000IR', 'M0000DT', none, none, none, none, none,none)
    FrenarRetorno()
    FrenarSeleccion()
    repeat
        wait 0 
        if EntrarAlMenu()
        then
            if $Seleccion == menu_del_jugador
            then {$I ./src/player/menu.txt}
            end   
            if $Seleccion == menu_de_los_actores
            then {$I ./src/actor/menu.txt} 
            end        
            if $Seleccion == menu_de_los_vehiculos
            then {$I ./src/vehicle/menu.txt} 
            end 
            if $Seleccion == menu_de_los_objetos
            then {$I ./src/object/menu.txt}   
            end                
            if $Seleccion == menu_de_la_camara
            then {$I ./src/camera/menu.txt}
            end  
            if $Seleccion == menu_del_juego
            then {$I ./src/game/menu.txt}   
            end
            FrenarSeleccion()
        end
    until SalirDelMenu()
    
    :CerrarElMenu
    EsconderMenu()
    Display.SetRadar(true)
    
    FrenarRetorno()
    
    if $canMove == false
    then
        $canMove = true 
        
        if $ModoDeCompilacion == ePara_PC
        then
            Player.SetCanEnterExitCars(_Char,true)
            Player.SetCanJump(_Char, true)
            Player.SetCanCrouch(_Char, true)  
            Player.SetAbleToShootWeapons(_Char, true)
            Player.SetCycleWeapon(_Char, true)
            Player.SetIgnoredByCops(_Char, false)
        end 
        
        if Actor.IsDriving(_Player)
        then 
            0@ = Actor.GetCurrentCar(_Player)
            Car.SetKeepPosition(0@, false)
            Car.SetCollision(0@, true) 
            Player.SetCanMove(_Char, true)
        else  
            if Player.IsControllable(_Char)
            then Player.SetCanMove(_Char, true)
            end
            Actor.SetCollisionDetection(_Player, true)
        end 
        
        Actor.SetKeepPosition(_Player, false)    
    end
    if $colPlayer == false
    then
        Actor.SetKeepPosition(_Player, true) 
        Actor.SetCollisionDetection(_Player, false) 
    end
     
    Text.UseCommands(false)     
    
    Text.ClearHelp()
return

{$I ./src/thread_externo.txt}
{$I ./src/thread_aislado.txt}
 