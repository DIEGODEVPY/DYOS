Menu.SetActiveItem($PANEL, 0)
:MenuDelObjeto
if
	$numObjetos >= 1
then
	if not Object.Defined($Objeto($indObjeto,101i))
	then
		$numObjetos--
		Object.Remove($Objeto($indObjeto,101i))
		$indObjeto--
		Wait0()
		jump @MenuDelActor
	end

	10@s = 'M0000M9'

	if
		$numObjetos >= 2
	then
		20@s = 'M00000D'
	else
		20@s = 'DUMMY'
	end
else
	10@s = 'DUMMY'
	20@s = 'DUMMY'
	Menu.SetActiveItem($PANEL, 0)
end

Menu.SetColumn($PANEL, 0, 'M0000DP','M0000RX',10@s,20@s,none,none,none,none,none,none,none,none,none)
FrenarRetorno()
FrenarSeleccion()
repeat
	wait 0
	if EntrarAlMenu()
	then
		if _Selected == 0 // Create
		then
			CrearObjeto() 
			if param1r == true
			then jump @MenuDeEdicionDelObjeto
			end
			if param1r == false
			then jump @ObjectDestroy
			end
			if param1r == undefined
			then jump @MenuDelObjeto
			end
		end

		if _Selected == 1 // Edit
		then
			Menu.SetActiveItem($PANEL, 0) 
			:MenuDeEdicionDelObjeto

			Object_Highlight()

			Menu.SetColumn($PANEL, 0, 'M0000DP','M0000RW','M0000NO','M0000NP','M00007Q','M00000E','M0000NK',none,none,none,none,none,none,none,none)
			FrenarRetorno()
			FrenarSeleccion() 
			repeat
				wait 0
				if EntrarAlMenu()
				then
					if _Selected == 0 // Solid
					then
						if $colObjeto($indObjeto,101i) == false
						then   
							Enable()
							$colObjeto($indObjeto,101i) = true
							Object.SetCollisionDetection($Objeto($indObjeto,101i), true)
						else   
							Disable()
							$colObjeto($indObjeto,101i) = false
							Object.SetCollisionDetection($Objeto($indObjeto,101i), false)
						end
					end

					if _Selected == 1 // Rompible
					then
						if i == false
						then
							i = true
							Enable()
						else
							i = false
							Disable()
						end
						Object.SetDestructible($Objeto($indObjeto,101i), i)
					end

					if _Selected == 2 // Recogible
					then
						if h == false
						then
							h = true
							Enable()
						else
							h = false
							Disable()
						end
						Object.SetLiftable($Objeto($indObjeto,101i), h)
					end

					if _Selected == 3 // Visible
					then
						if $visObjeto($indObjeto,101i) == true
						then
							Disable()
							$visObjeto($indObjeto,101i) = false
							Object.SetVisibility($Objeto($indObjeto,101i), false)
						else
							Enable()
							$visObjeto($indObjeto,101i) = true
							Object.SetVisibility($Objeto($indObjeto,101i), true)
						end
					end

					if _Selected == 4 // Location
					then 
						{$I ./location.txt}
						Menu.SetActiveItem($PANEL, 4)
						jump @MenuDeEdicionDelObjeto
					end

					if _Selected == 5 // Destroy
					then 
						if $indObjeto == $numObjetos
						then
							:ObjectDestroy
							$numObjetos--
							Object.Remove($Objeto($indObjeto,101i))
							$indObjeto--

							while not Object.Defined($Objeto($indObjeto,101i))
								$indObjeto--
								$numObjetos--
								if and
									$indObjeto < 0
									$numObjetos < 0
								then
									$indObjeto = 0
									$numObjetos = 0
									break
								end
								//Wait0()
							end
							
							if $numObjetos == 0
							then				 
								Text.PrintHelp('M00000C')
								jump @MenuDelObjeto
							else
								Menu.SetActiveItem($PANEL, 5)
								Aplicated()
								jump @MenuDeEdicionDelObjeto
							end
						else
							Object.Remove($Objeto($indObjeto,101i))
							$indObjeto = $numObjetos
							Blip.Remove($ResaltadorDeEntidad)
							Menu.SetActiveItem($PANEL, 5)
							Aplicated()
							jump @MenuDeEdicionDelObjeto
						end
					end 
					FrenarSeleccion() 
				end
			until SalirDelMenu()
			if Object.Defined($Objeto($indObjeto,101i))
			then Blip.Remove($ResaltadorDeEntidad)
			end
			Menu.SetActiveItem($PANEL, 1)
			jump @MenuDelObjeto
		end   

		if _Selected == 2 // Select
		then
			if $ModoDeCompilacion == ePara_PC
			then Text.PrintHelp('M0000JK')
			else Text.PrintHelp('M0000JL')
			end
			EsconderMenu()
			FrenarSeleccion()
			repeat
				Wait0()
				Text.PrintWith2NumbersNow('M0000BG',$indObjeto,$numObjetos,150,0)

				if ParaEspecial()
				then
					$indObjeto = 0
					float 10@ = 1.0
					int 12@ = false
					while true 
						Wait0()
						Text.Print('M0000MX',10,0)
						12@ = true
						$indObjeto++
						if $indObjeto > $numObjetos
						then
							$indObjeto = 1
							10@ += 1.0
							if 10@ > 75.0
							then
								Text.ClearHelp()
								Text.PrintHelp('M0000L6')
								12@ = false
								break
							end
						end
						if Object.Defined($Objeto($indObjeto,101i))
						then
							if Actor.IsLocatedNearObject3D(_Player,0,$Objeto($indObjeto,101i),10@,10@,10@)
							then break
							end
						end
					end
					Blip.Remove($ResaltadorDeEntidad)
					if 12@ == true
					then
						AparecerMenu()
						Menu.SetActiveItem($PANEL, 0)
						Text.ClearHelp()
						Text.ClearPrints()
						jump @MenuDeEdicionDelObjeto
					end
				end

				if Blip.Defined($ResaltadorDeEntidad)
				then
					Wait0()
				else 
					$ResaltadorDeEntidad = Blip.CreateAboveObject($Objeto($indObjeto,101i))
					while ParaIzquierda() 
						Wait0()
					end
					while ParaDerecha() 
						Wait0()
					end
				end

				if ParaIzquierda()
				then
					ObjectDefinidoInferior()
					Blip.Remove($ResaltadorDeEntidad)
				end
				if ParaDerecha()
				then
					ObjectDefinidoSuperior()
					Blip.Remove($ResaltadorDeEntidad)
				end

				if Selecionas()
				then
					AparecerMenu()
					Menu.SetActiveItem($PANEL, 0)
					Text.ClearHelp()
					Text.ClearPrints()
					jump @MenuDeEdicionDelObjeto
				end
			until SalirDelMenu()
			Blip.Remove($ResaltadorDeEntidad)
			AparecerMenu()
			Menu.SetActiveItem($PANEL, 2)
			jump @MenuDelObjeto
		end
		FrenarSeleccion()
	end
until SalirDelMenu()

Menu.SetActiveItem($PANEL, 3)	  
jump @MenuPrincipal