Menu.SetActiveItem($PANEL, 0)
:MenuDelAuto
if $numAutos == 0
then
    Menu.SetColumn($PANEL, 0, 'VVEHICL', 'ZUIZCRE',none,none,none,none,none,none,none,none,none,none,none)
    FrenarRetorno()
    FrenarSeleccion()
    repeat
        wait 0
        if EntrarAlMenu()
        then
            if $Seleccion == 0
            then
                CrearAuto() 
                FrenarSeleccion()
                jump @MenuDelAuto
            end
        end
    until SalirDelMenu()
    Menu.SetActiveItem($PANEL, 1)
else 
    if $numAutos > 1
    then 0@s = 'M00000D'
    else 0@s = 'DUMMY'
    end
    Menu.SetColumn($PANEL, 0, 'VVEHICL','ZUIZCRE', 'ZUIZEDI',0@s,none,none,none,none,none,none,none,none,none)
    FrenarRetorno()
    FrenarSeleccion()
    repeat
        wait 0
        if EntrarAlMenu()
        then
            if $Seleccion == 0
            then
                CrearAuto() 
                jump @MenuDelAuto
            end 

            if $Seleccion == 1 // Edit
            then
                Menu.SetActiveItem($PANEL, 0) 
                :MenuDeEdicionDelAuto

                if Car.Defined($Auto($indAuto,101i))
                then
                    if Blip.Defined($ResaltadorDeEntidad)
                    then wait 0
                    else $ResaltadorDeEntidad = Blip.CreateAboveCar($Auto($indAuto,101i))
                    end
                end

                if $indAuto == $numAutos
                then 0@s = 'ZUIZDES'
                else 0@s = none
                end
                
                Menu.SetColumn($PANEL, 0, 'VVEHICL','ZUIZSOL','M00000E','M00007Q',0@s,none,none,none,none,none,none,none,none)
                FrenarRetorno()
                FrenarSeleccion()  
                repeat
                    wait 0
                    if EntrarAlMenu()
                    then
                        if $Seleccion == 0 // Solid
                        then
                            if $colAuto($indAuto,101i) == false
                            then 
                                Text.PrintHelp('ZBZENAB') 
                                $colAuto($indAuto,101i) = true
                                Car.SetKeepPosition($Auto($indAuto,101i), false) 
                                Car.SetImmunities($Auto($indAuto,101i), false, false, false, false, false)
                                Car.SetCollision($Auto($indAuto,101i), true)
                            else 
                                Text.PrintHelp('ZBZDISA')
                                $colAuto($indAuto,101i) = false 
                                Car.SetKeepPosition($Auto($indAuto,101i), true) 
                                Car.SetImmunities($Auto($indAuto,101i), true, true, true, true, true)
                                Car.SetCollision($Auto($indAuto,101i), false)
                            end
                        end
                        
                        if $Seleccion == 1 // Location
                        then    
                            Text.ClearHelp()
                            EsconderMenu()

                            s = 0       
                            FrenarSeleccion()
                            repeat
                                wait 0   
                                Txd.DrawRect(60.0, 60.0, 250.0, 140.0, 0x0,0x0,0x0,0xCC)
                                                      
                                Text.SetScale(0.4, 1.1)
                                Text.Display(10.0, 20.0, 'M00001C')
                                    
                                if s == 0
                                then                                      
                                    Car.GetPositionAndOffset(x, y, z, $Auto($indAuto,101i), 0.0, 0.1364, -0.0)
                                    
                                    0@ = Math.FloatToInt_LsG(x)      
                                    Text.DisplayWithNumber(10.0, 80.0, 'M000011', 0@)
                                    0@ = Math.FloatToInt_LsG(y)    
                                    Text.DisplayWithNumber(10.0, 90.0, 'M000012', 0@)
                                    0@ = Math.FloatToInt_LsG(z)
                                    Text.DisplayWithNumber(10.0, 100.0, 'M000013', 0@)
                                    
                                    ControlDeDesplazamiento3D()
                                          
                                    Car.SetPosition($Auto($indAuto,101i), x, y, z)
                                    if Selecionas()
                                    then
                                        s = 1
                                        while Selecionas()
                                            wait 0
                                        end 
                                    end
                                end
                                if s == 1
                                then                                       
                                    a = Car.GetAngle($Auto($indAuto,101i))
                                    
                                    0@ = Math.FloatToInt_LsG(a)      
                                    Text.DisplayWithNumber(10.0, 80.0, 'M000011', 0@)
                                              
                                    
                                    if ParaDerecha() 
                                    then a -= 1.0
                                    end
                                    
                                    if ParaIzquierda()
                                    then a += 1.0
                                    end 
                                          
                                    Car.SetAngle($Auto($indAuto,101i), a) 
                                    if Selecionas()
                                    then
                                        s = 0
                                        while Selecionas()
                                            wait 0
                                        end 
                                    end
                                end
                            until SalirDelMenu()
                            AparecerMenu()
                            Menu.SetActiveItem($PANEL, 1)
                            jump @MenuDeEdicionDelAuto 
                        end 

                        if $Seleccion == 2 // Visibility
                        then
                            if $visAuto($indAuto,101i) == true
                            then
                                Text.PrintHelp('ZBZDISA')
                                $visAuto($indAuto,101i) = false
                                Car.SetVisibility($Auto($indAuto,101i), false)
                            else
                                Text.PrintHelp('ZBZENAB')
                                $visAuto($indAuto,101i) = true
                                Car.SetVisibility($Auto($indAuto,101i), true)
                            end
                        end 

                        if $Seleccion == 3 // Destroy
                        then
                            $numAutos--
                            Car.Remove($Auto($indAuto,101i))
                            $indAuto--
                            if $numAutos == 0
                            then                
                                Text.PrintHelp('M00000B') 
                                Menu.SetActiveItem($PANEL, 0)
                                jump @MenuDelAuto
                            else
                                Menu.SetActiveItem($PANEL, 3)
                                jump @MenuDeEdicionDelAuto
                            end 
                        end 
                        FrenarSeleccion() 
                    end
                until SalirDelMenu() 
                if Car.Defined($Auto($indAuto,101i))
                then Blip.Remove($ResaltadorDeEntidad)
                end
                Menu.SetActiveItem($PANEL, 1)
                jump @MenuDelAuto
            end

            if $Seleccion == 2 // Select
            then
                EsconderMenu()
                repeat
                    wait 0
                    Text.PrintWith2NumbersNow('ZCLC',$indAuto,$numAutos,150,0)

                    if Blip.Defined($ResaltadorDeEntidad)
                    then
                        wait 0
                    else 
                        $ResaltadorDeEntidad = Blip.CreateAboveCar($Auto($indAuto,101i))
                        while ParaIzquierda() 
                            wait 0
                        end
                        while ParaDerecha() 
                            wait 0
                        end
                    end

                    if $numAutos > 1
                    then
                        if ParaIzquierda()
                        then
                            $indAuto--
                            if $indAuto < 1
                            then $indAuto = $numAutos 
                            end
                            Blip.Remove($ResaltadorDeEntidad)
                        end
                        if ParaDerecha()
                        then
                            $indAuto++
                            if $indAuto > $numAutos
                            then $indAuto = 1
                            end
                            Blip.Remove($ResaltadorDeEntidad)
                        end
                    end
                until SalirDelMenu()
                Blip.Remove($ResaltadorDeEntidad)
                AparecerMenu()
                Menu.SetActiveItem($PANEL, 2)
                jump @MenuDelAuto
            end
            FrenarSeleccion()
        end
    until SalirDelMenu()
end
Menu.SetActiveItem($PANEL, 2)
jump @MenuPrincipal  