Menu.SetActiveItem($PANEL, 0)
:MenuDelAuto
if
    $numAutos >= 1
then
    10@s = 'ZUIZEDI'

    if
        $numAutos >= 2
    then
        20@s = 'M00000D'
    else
        20@s = 'DUMMY'
    end
else
    10@s = 'DUMMY'
    20@s = 'DUMMY'
end

Menu.SetColumn($PANEL, 0, 'M0000DR','ZUIZCRE',10@s,20@s,none,none,none,none,none,none,none,none,none)
FrenarRetorno()
FrenarSeleccion()
repeat
    wait 0
    if EntrarAlMenu()
    then
        if $Seleccion == 0
        then
            CrearAuto()
            jump @MenuDelAuto
        end 

        if $Seleccion == 1 // Edit
        then
            Menu.SetActiveItem($PANEL, 0) 
            :MenuDeEdicionDelAuto

            if Car.Defined($Auto($indAuto,101i))
            then
                if Blip.Defined($ResaltadorDeEntidad)
                then wait 0
                else $ResaltadorDeEntidad = Blip.CreateAboveCar($Auto($indAuto,101i))
                end
            end

            if $indAuto == $numAutos
            then 0@s = 'ZUIZDES'
            else
                Text.PrintHelp('M0000A0')
                0@s = none
            end
            
            Menu.SetColumn($PANEL, 0, 'M0000DR','ZUIZSOL','M000095','M0000A6','M0000AJ','M00007Q','M00000E',0@s,none,none,none,none,none)
            FrenarRetorno()
            FrenarSeleccion()  
            repeat
                wait 0
                if EntrarAlMenu()
                then
                    if $Seleccion == 0 // Solid
                    then
                        if $colAuto($indAuto,101i) == false
                        then 
                            Enable()
                            $colAuto($indAuto,101i) = true
                            Car.SetKeepPosition($Auto($indAuto,101i), false)
                            Car.SetCollision($Auto($indAuto,101i), true)
                        else 
                            Disable()
                            $colAuto($indAuto,101i) = false 
                            Car.SetKeepPosition($Auto($indAuto,101i), true)
                            Car.SetCollision($Auto($indAuto,101i), false)
                        end
                    end

                    if $Seleccion == 1 // Door
                    then {$I ./door/menu.txt}
                        Menu.SetActiveItem($PANEL,1)
                        jump @MenuDeEdicionDelAuto
                    end

                    if $Seleccion == 2 // Tyre
                    then
                        FrenarSeleccion()
                        Menu.SetActiveItem($PANEL,0)
                        Menu.SetColumn($PANEL,0,'M0000A6','M00009H','M00009I','M00009J','M00009K',none,none,none,none,none,none,none,none)
                        repeat
                            wait 0
                            if EntrarAlMenu()
                            then
                                if $Seleccion == 0
                                then
                                    if Car.IsTyreBurst($Auto($indAuto,101i), CarTyre.FrontLeft)
                                    then Car.FixTyre($Auto($indAuto,101i), CarTyre.FrontLeft)
                                    else Car.SetBurstTyre($Auto($indAuto,101i), CarTyre.FrontLeft)
                                    end
                                end
                                if $Seleccion == 1
                                then
                                    if Car.IsTyreBurst($Auto($indAuto,101i), CarTyre.FrontRight)
                                    then Car.FixTyre($Auto($indAuto,101i), CarTyre.FrontRight)
                                    else Car.SetBurstTyre($Auto($indAuto,101i), CarTyre.FrontRight)
                                    end
                                end
                                if $Seleccion == 2
                                then
                                    if Car.IsTyreBurst($Auto($indAuto,101i), CarTyre.RearLeft)
                                    then Car.FixTyre($Auto($indAuto,101i), CarTyre.RearLeft)
                                    else Car.SetBurstTyre($Auto($indAuto,101i), CarTyre.RearLeft)
                                    end
                                end
                                if $Seleccion == 3
                                then
                                    if Car.IsTyreBurst($Auto($indAuto,101i), CarTyre.RearRight)
                                    then Car.FixTyre($Auto($indAuto,101i), CarTyre.RearRight)
                                    else Car.SetBurstTyre($Auto($indAuto,101i), CarTyre.RearRight)
                                    end
                                end
                                FrenarSeleccion()
                                Aplicated()
                            end
                        until SalirDelMenu()
                        Menu.SetActiveItem($PANEL,2)
                        jump @MenuDeEdicionDelAuto
                    end

                    if $Seleccion == 3 // Health
                    then
                        Menu.SetActiveItem($PANEL, 0)
                        Menu.SetColumn($PANEL,0,'M0000AJ','M0000A8','M0000AF','M0000AG','M0000AH','M0000AI','M0000AD',none,none,none,none,none,none)
                        FrenarSeleccion()
                        repeat
                            wait 0
                            if EntrarAlMenu()
                            then 
                                if $Seleccion == 0
                                then Car.SetHealth($Auto($indAuto,101i), 0)
                                end
                                if $Seleccion == 1
                                then Car.SetHealth($Auto($indAuto,101i), 250)
                                end
                                if $Seleccion == 2
                                then Car.SetHealth($Auto($indAuto,101i), 500)
                                end
                                if $Seleccion == 3
                                then Car.SetHealth($Auto($indAuto,101i), 750)
                                end
                                if $Seleccion == 4
                                then Car.SetHealth($Auto($indAuto,101i), 1000)
                                end   
                                if $Seleccion == 5
                                then
                                    if $indAuto($indAuto,101i) == false
                                    then
                                        $indAuto($indAuto,101i) = true
                                        Car.SetImmunities($Auto($indAuto,101i),1,1,1,1,1)
                                    else
                                        $indAuto($indAuto,101i) = false
                                        Car.SetImmunities($Auto($indAuto,101i),0,0,0,0,0)
                                    end
                                end
                                Aplicated() 
                                FrenarSeleccion()
                            end
                        until SalirDelMenu()
                        Menu.SetActiveItem($PANEL, 3)
                        jump @MenuDeEdicionDelAuto
                    end

                    if $Seleccion == 4 // Visibility
                    then
                        if $visAuto($indAuto,101i) == true
                        then
                            Enable()
                            $visAuto($indAuto,101i) = false
                            Car.SetVisibility($Auto($indAuto,101i), false)
                        else
                            Disable()
                            $visAuto($indAuto,101i) = true
                            Car.SetVisibility($Auto($indAuto,101i), true)
                        end
                    end
                    
                    if $Seleccion == 5 // Location
                    then
                        Text.ClearHelp()

                        //Camera.AttachToCar($Auto($indAuto,101i),0.0,-1.7,2.0,0.0,0.0,0.0,0.0,SwitchType.JumpCut)

                        EsconderMenu()
                        s = 0       
                        FrenarSeleccion()

                        Car.GetPositionAndOffset(x, y, z, $Auto($indAuto,101i), 0.0, 0.0, 0.0)
                        repeat
                            wait 0   
                            Txd.DrawRect(90.0, 60.0, 180.0, 140.0, 0x0,0x0,0x0,0xCC)
                                                  
                            Text.SetScale(0.4, 1.1)
                            Text.Display(10.0, 20.0, 'M00001C')
                                
                            if s == 0
                            then
                                0@ = Math.FloatToInt_LsG(x)      
                                Text.DisplayWithNumber(10.0, 80.0, 'M000011', 0@)
                                0@ = Math.FloatToInt_LsG(y)    
                                Text.DisplayWithNumber(10.0, 90.0, 'M000012', 0@)
                                
                                if ParaDerecha()
                                then x += 0.05
                                end
                                if ParaIzquierda()
                                then x -= 0.05
                                end  
                                if ParaAdelante()
                                then y += 0.05
                                end
                                if ParaAtras()
                                then y -= 0.05
                                end
                                      
                                Car.SetPosition($Auto($indAuto,101i), x, y, z)
                                if Selecionas()
                                then
                                    s = 1
                                    while Selecionas()
                                        wait 0
                                    end 
                                end
                            end
                            if s == 1
                            then                                       
                                a = Car.GetAngle($Auto($indAuto,101i))
                                
                                0@ = Math.FloatToInt_LsG(a)      
                                Text.DisplayWithNumber(10.0, 80.0, 'M000016', 0@)
                                0@ = Math.FloatToInt_LsG(z)
                                Text.DisplayWithNumber(10.0, 90.0, 'M000013', 0@)
                                
                                if ParaDerecha() 
                                then a -= 1.0
                                end
                                if ParaIzquierda()
                                then a += 1.0
                                end
                                if ParaAdelante()
                                then z += 0.05
                                end
                                if ParaAtras()
                                then z -= 0.05
                                end
                                      
                                Car.SetPosition($Auto($indAuto,101i), x, y, z)
                                Car.SetAngle($Auto($indAuto,101i), a) 
                                if Selecionas()
                                then
                                    s = 0
                                    while Selecionas()
                                        wait 0
                                    end 
                                end
                            end
                        until SalirDelMenu()
                        AparecerMenu()
                        Menu.SetActiveItem($PANEL, 5)
                        //Camera.Restore()
                        jump @MenuDeEdicionDelAuto 
                    end 

                    if $Seleccion == 6 // Destroy
                    then
                        $numAutos--
                        Car.Remove($Auto($indAuto,101i))
                        $indAuto--
                        if $numAutos == 0
                        then                
                            Text.PrintHelp('M00000B') 
                            Menu.SetActiveItem($PANEL, 0)
                            jump @MenuDelAuto
                        else
                            Menu.SetActiveItem($PANEL, 6)
                            jump @MenuDeEdicionDelAuto
                        end 
                    end 
                    FrenarSeleccion() 
                end
            until SalirDelMenu() 
            if Car.Defined($Auto($indAuto,101i))
            then Blip.Remove($ResaltadorDeEntidad)
            end
            Menu.SetActiveItem($PANEL, 1)
            jump @MenuDelAuto
        end

        if $Seleccion == 2 // Select
        then
            EsconderMenu()
            FrenarSeleccion()
            repeat
                wait 0
                Text.PrintWith2NumbersNow('M0000BG',$indAuto,$numAutos,150,0)

                if Blip.Defined($ResaltadorDeEntidad)
                then
                    wait 0
                else 
                    $ResaltadorDeEntidad = Blip.CreateAboveCar($Auto($indAuto,101i))
                    while ParaIzquierda() 
                        wait 0
                    end
                    while ParaDerecha() 
                        wait 0
                    end
                end

                if $numAutos > 1
                then
                    if ParaIzquierda()
                    then
                        $indAuto--
                        if $indAuto < 1
                        then $indAuto = $numAutos 
                        end
                        Blip.Remove($ResaltadorDeEntidad)
                    end
                    if ParaDerecha()
                    then
                        $indAuto++
                        if $indAuto > $numAutos
                        then $indAuto = 1
                        end
                        Blip.Remove($ResaltadorDeEntidad)
                    end
                    if Selecionas()
                    then
                        AparecerMenu()
                        Menu.SetActiveItem($PANEL, 0)
                        Text.ClearHelp()
                        Text.ClearPrints()
                        jump @MenuDeEdicionDelAuto
                    end
                end
            until SalirDelMenu()
            Blip.Remove($ResaltadorDeEntidad)
            AparecerMenu()
            Menu.SetActiveItem($PANEL, 2)
            jump @MenuDelAuto
        end
        FrenarSeleccion()
    end
until SalirDelMenu()

Menu.SetActiveItem($PANEL, 2)
jump @MenuPrincipal  