Menu.SetActiveItem($PANEL, 0)
:MenuDeEscenarios
Menu.SetColumn($PANEL,0,'M0000Q6','M0000DU','M0000Q7','M0000M8',none,none,none,none,none,none,none,none,none)
FrenarSeleccion()
FrenarRetorno()
repeat
	Wait0()
	if EntrarAlMenu()
	then
		//{$I ../../src/const.txt}

		
		if _Selected == 0 // SAVE SCENE
		then
			Text.PrintHelpForever('M0000M7')
			{$I ./save/main.txt}

			if $numActores > 0
			then
				int 5@ = 0
				$indActor = 0

				while $numActores > 5@
					Wait0()
					int 5@ += 1
					$indActor += 1

					10@ = Actor.GetModel($Actor($indActor,255i))
					11@ = Actor.GetPedtype($Actor($indActor,255i))

					Actor.GetPositionAndOffset(12@,13@,14@,$Actor($indActor,255i),0.0,0.0,-1.0)
					15@ = Actor.GetAngle($Actor($indActor,255i))

					int 20@ = $statsActor($indActor,255i)

					21@ = Actor.GetHealth($Actor($indActor,255i))
					22@ = Actor.GetArmour($Actor($indActor,255i))

					23@ = Actor.GetCurrentWeapon($Actor($indActor,255i))

					int 0@ = 5@
					bget_actor()

					int 28@ = 0@
					{$I ./save/actor.txt}
				end
			end

			if $numAutos > 0
			then
				int 5@ = 0
				$indAuto = 0

				while $numAutos > 5@
					Wait0()
					5@ += 1
					$indAuto += 1

					6@ = Car.GetModel($Auto($indAuto,255i))
					Car.GetPosition($Auto($indAuto,255i),7@,8@,9@)
					Car.GetQuaternion($Auto($indAuto,255i),10@,11@,12@,13@)
					14@ = Car.GetPaintjob($Auto($indAuto,255i))
					Car.GetColorA($Auto($indAuto,255i),15@,16@)
					Car.GetColorB($Auto($indAuto,255i),17@,18@)
					19@ = Car.GetHealth($Auto($indAuto,255i))
					{$I ./save/vehicle.txt}
				end
			end

			if $numObjetos > 0
			then
				int 5@ = 0
				$indObjeto = 0

				while $numObjetos > 5@
					Wait0()
					5@ += 1
					$indObjeto += 1

					6@ = Object.GetModel($Objeto($indObjeto,255i))
					Object.GetPosition($Objeto($indObjeto,255i),7@,8@,9@)
					Object.GetQuaternion($Objeto($indObjeto,255i),10@,11@,12@,13@)
					{$I ./save/object.txt}
				end
			end

			Text.ClearHelp()
			Aplicated()
		end

		if _Selected == 1 // LOAD SCENE
		then
			int 0@ = IniFile.GetInt("./dyos.ini","main","saved")
			if 0@ == true
			then
				float 12@ = IniFile.GetFloat("./dyos.ini","camera","pos.x")
				float 13@ = IniFile.GetFloat("./dyos.ini","camera","pos.y")
				float 14@ = IniFile.GetFloat("./dyos.ini","camera","pos.z")
				float 15@ = IniFile.GetFloat("./dyos.ini","camera","see.x")
				float 16@ = IniFile.GetFloat("./dyos.ini","camera","see.y")
				float 17@ = IniFile.GetFloat("./dyos.ini","camera","see.z")
				Camera.SetVectorMove(12@,13@,14@,12@,13@,14@,10,SwitchType.JumpCut)
				Camera.SetVectorTrack(15@,16@,17@,15@,16@,17@,10,SwitchType.JumpCut)

				{$I ./load/main.txt}

				while $numActores > 0
					Wait0()
					$indActor = $numActores
					Actor.Remove($Actor($indActor,255i))
					$numActores -= 1
				end
				while $numAutos > 0
					Wait0()
					$indAuto = $numAutos
					Car.Remove($Auto($indAuto,255i))
					$numAutos -= 1
				end
				while $numObjetos > 0
					Wait0()
					$indObjeto = $numObjetos
					Object.Remove($Objeto($indObjeto,255i))
					$numObjetos -= 1
				end

				$numActores = IniFile.GetInt("./dyos.ini","numbers","actors")
				if $numActores > 0
				then
					int 5@ = 0
					$indActor = 0

					while $indActor < $numActores
						Wait0()
						5@ += 1
						$indActor += 1

						{$I ./load/actor.txt}

						if 11@ == 23
						then
							Model.Load(7)
							repeat
								Wait0()
							until Model.Available(7)
							$Actor($indActor,255i) = Actor.Create(4, 7, 12@, 13@, 14@)
							Model.Release(7)
						else
							Model.Load(6@)
							repeat
								Wait0()
							until Model.Available(6@)
							$Actor($indActor,255i) = Actor.Create(11@, 10@, 12@, 13@, 14@)
							Model.Release(6@)
						end

						Actor.Angle($Actor($indActor,255i)) = 15@
						
						int $statsActor($indActor,255i) = 20@
						if 08B4: $statsActor($indActor,255i) is_true ACTOR_PROP_COLISION // is TRUE
						then
							Actor.SetKeepPosition($Actor($indActor,255i), false)
							Actor.SetCollisionDetection($Actor($indActor,255i), true)

							if 88B4: $statsActor($indActor,255i) is_false ACTOR_PROP_FREEZE_POSITION
							then Actor.SetLockInCurrentPosition($Actor($indActor,255i), false)
							else Actor.SetLockInCurrentPosition($Actor($indActor,255i), true)
							end
						else
							Actor.SetKeepPosition($Actor($indActor,255i), true)
							Actor.SetCollisionDetection($Actor($indActor,255i), false)
						end

						if 88B4: $statsActor($indActor,255i) is_false ACTOR_PROP_IMMUNITION_BULLETS // bit 1 == false
						then 10@ = false
						else 10@ = true
						end
						if 88B4: $statsActor($indActor,255i) is_false ACTOR_PROP_IMMUNITION_FIRE // bit 1 == false
						then 11@ = false
						else 11@ = true
						end
						if 88B4: $statsActor($indActor,255i) is_false ACTOR_PROP_IMMUNITION_EXPLOSION // bit 1 == false
						then 12@ = false
						else 12@ = true
						end
						if 88B4: $statsActor($indActor,255i) is_false ACTOR_PROP_IMMUNITION_COLLISION // bit 1 == false
						then 13@ = false
						else 13@ = true
						end
						if 88B4: $statsActor($indActor,255i) is_false ACTOR_PROP_IMMUNITION_MELEE // bit 1 == false
						then 14@ = false
						else 14@ = true
						end
						if 88B4: $statsActor($indActor,255i) is_false ACTOR_PROP_DROWN_AT_WATER // bit 1 == false
						then 15@ = false
						else 15@ = true
						end
						Actor.SetImmunities($Actor($indActor,255i),10@,11@,12@,13@,14@)
						Actor.SetDrownAtWater($Actor($indActor,255i),15@)

						if 08B4: $statsActor($indActor,255i) is_true ACTOR_PROP_BLEEDING // bit 1 == true
						then Actor.SetBleeding($Actor($indActor,255i), true)
						else Actor.SetBleeding($Actor($indActor,255i), false)
						end

						if 08B4: $statsActor($indActor,255i) is_true ACTOR_PROP_VISION // is TRUE
						then Actor.SetVisibility($Actor($indActor,255i), true)
						else Actor.SetVisibility($Actor($indActor,255i), false)
						end

						Actor.SetHealth($Actor($indActor,255i), 21@)
						Actor.SetMaxHealth($Actor($indActor,255i),21@)
						Actor.SetAddArmour($Actor($indActor,255i), 22@)


						if 28@ >= 0
						then
							$CURRENT_ANIM_ID = 28@
							AnimDyom()
							
							if $ANIM_NOT_IS_OF_PED == false
							then
								Task.PlayAnim($Actor($indActor,255i), v$IFP_ANIM, "PED", 30.0, true, false, false, false, -1)
							else
								Animation.Load(v$IFP_FILE)
								repeat
									Wait0()
								until Animation.Available(v$IFP_FILE)
								Task.PlayAnim($Actor($indActor,255i), v$IFP_ANIM, v$IFP_FILE, 30.0, true, false, false, false, -1)
								Animation.Remove(v$IFP_FILE)
							end

							08BA: $statsActor($indActor,255i) set_true ACTOR_PROP_PLAY_ANIM_DYOM // bit 1 = true

							int 0@ = $indActor
							int 1@ = $CURRENT_ANIM_ID
							bset_actor()
						else
							08C0: $statsActor($indActor,255i) set_false ACTOR_PROP_PLAY_ANIM_DYOM // bit 1 = false
							int 0@ = $indActor
							1@ = -1
							bset_actor()
						end

						10@ = IniFile.GetInt("./dyos.ini","game","set.interior")
						Actor.SetLinkInInterior($Actor($indActor,255i), 10@)
					end
				end

				$numAutos = IniFile.GetInt("./dyos.ini","numbers","vehicles")
				if $numAutos > 0
				then
					int 5@ = 0
					$indAuto = 0

					while $indAuto < $numAutos
						Wait0()
						5@ += 1
						$indAuto += 1

						{$I ./load/vehicle.txt}

						Model.Load(6@)
						repeat
							Wait0()
						until Model.Available(6@)
						$Auto($indAuto,255i) = Car.Create(6@, 7@, 8@, 9@)
						Model.Release(6@)
						Car.SetQuaternion($Auto($indAuto,255i),10@,11@,12@,13@)

						if 14@ >= 0
						then Car.SetPaintjob($Auto($indAuto,255i),14@)
						end

						if $colAuto($indAuto,255i) == false
						then
							Car.SetKeepPosition($Auto($indAuto,255i), true)
							Car.SetCollision($Auto($indAuto,255i), false)
						end

						6@ = IniFile.GetInt("./dyos.ini","game","set.interior")
						Car.SetLinkInInterior($Auto($indAuto,255i), 6@)
						
						Car.SetVisibility($Auto($indAuto,255i), $visAuto($indAuto,255i))

						Car.SetHealth($Auto($indAuto,255i), 19@)
						if $immAuto($indAuto,255i) == true
						then Car.SetImmunities($Auto($indAuto,255i),1,1,1,1,1)
						end
						Car.SetColorA($Auto($indAuto,255i),15@,16@)
						if 17@ >= 1
						then Car.SetColorB($Auto($indAuto,255i),17@,18@)
						end
					end
				end

				$numObjetos = IniFile.GetInt("./dyos.ini","numbers","objects")
				if $numObjetos > 0
				then
					int 5@ = 0
					$indObjeto = 0

					while $indObjeto < $numObjetos
						Wait0()
						5@ += 1
						$indObjeto += 1

						{$I ./load/object.txt}

						Model.Load(6@)
						repeat
							Wait0()
						until Model.Available(6@)
						$Objeto($indObjeto,255i) = Object.Create(6@, 7@, 8@, 9@)
						Model.Release(6@)
						Object.SetQuaternion($Objeto($indObjeto,255i),10@,11@,12@,13@)

						if $colObjeto($indObjeto,255i) == false
						then Object.SetCollisionDetection($Objeto($indObjeto,255i), false)
						end

						6@ = IniFile.GetInt("./dyos.ini","game","set.interior")
						Object.SetLinkInInterior($Objeto($indObjeto,255i), 6@)
						
						Object.SetVisibility($Objeto($indObjeto,255i), $visObjeto($indObjeto,255i))
					end
				end

				float 12@ = IniFile.GetFloat("./dyos.ini","camera","pos.x")
				float 13@ = IniFile.GetFloat("./dyos.ini","camera","pos.y")
				float 14@ = IniFile.GetFloat("./dyos.ini","camera","pos.z")
				float 15@ = IniFile.GetFloat("./dyos.ini","camera","see.x")
				float 16@ = IniFile.GetFloat("./dyos.ini","camera","see.y")
				float 17@ = IniFile.GetFloat("./dyos.ini","camera","see.z")
				Camera.SetVectorMove(12@,13@,14@,12@,13@,14@,10,SwitchType.JumpCut)
				Camera.SetVectorTrack(15@,16@,17@,15@,16@,17@,10,SwitchType.JumpCut)

				IniFile.GetInt(18@,"./dyos.ini","camera","set.see.ent")
				IniFile.GetInt(19@,"./dyos.ini","camera","set.see.num")
				if $ModoDeCompilacion == ePara_PC
				then
					set_cleo_shared_var CAMARA_MIRAR = 18@
					set_cleo_shared_var CAMARA_MIRAR_ENTIDAD = 19@
				else
					Android.SetVar(CAMARA_MIRAR,18@)
					Android.SetVar(CAMARA_MIRAR_ENTIDAD,19@)
				end
				Wait0()


				Aplicated()
			else Text.PrintHelp('M00001C')
			end
		end
		

		if _Selected == 2 // LOAD CUTSCENE
		then
			{$I ./cutscene.txt}
			Menu.SetActiveItem($PANEL,2)
			$Numero_De_Paginas = 1
			$Pagina_Actual = 1
			jump @MenuDeEscenarios
		end
		FrenarSeleccion()
	end
until SalirDelMenu()  

Menu.SetActiveItem($PANEL, menu_del_escenario)
jump @MenuPrincipal